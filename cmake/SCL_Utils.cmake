
MACRO(DEFINE_DLL_EXPORTS libname)
    IF( MSVC OR BORLAND )
        if( ${libname} MATCHES "SDAI_.*" )
            set( export "SCL_SCHEMA_DLL_EXPORTS" )
        else()
            STRING(REGEX REPLACE "lib" "" shortname "${libname}")
            STRING(REGEX REPLACE "step" "" LOWERCORE "${shortname}")
            STRING(TOUPPER ${LOWERCORE} UPPER_CORE)
            set( export "SCL_${UPPER_CORE}_DLL_EXPORTS" )
        endif()
        get_target_property(defs ${libname} COMPILE_DEFINITIONS )
        if( defs ) #if no properties, ${defs} will be defs-NOTFOUND which CMake interprets as false
            set( defs "${defs};${export}")
        else( defs )
            set( defs "${export}")
        endif( defs )
        set_target_properties(${libname} PROPERTIES COMPILE_DEFINITIONS "${defs}" )
    ENDIF( MSVC OR BORLAND )
ENDMACRO(DEFINE_DLL_EXPORTS libname)

MACRO( DEFINE_DLL_IMPORTS tgt libs )
    IF( MSVC OR BORLAND )
        get_target_property(defs ${tgt} COMPILE_DEFINITIONS )
        if( NOT defs ) #if no properties, ${defs} will be defs-NOTFOUND which CMake interprets as false
            set( defs "")
        endif( NOT defs )
        foreach( lib ${libs} )
            STRING(REGEX REPLACE "lib" "" shortname "${lib}")
            STRING(REGEX REPLACE "step" "" LOWERCORE "${shortname}")
            STRING(TOUPPER ${LOWERCORE} UPPER_CORE)
            list( APPEND defs "SCL_${UPPER_CORE}_DLL_IMPORTS" )
        endforeach( lib ${libs} )
        if( DEFINED defs )
            if( defs )
                set_target_properties( ${tgt} PROPERTIES COMPILE_DEFINITIONS "${defs}" )
            endif( defs )
        endif( DEFINED defs )
    ENDIF( MSVC OR BORLAND )
ENDMACRO( DEFINE_DLL_IMPORTS tgt libs )

MACRO(SCL_ADDEXEC execname srcs libs)
    STRING(REGEX REPLACE " " ";" srcslist "${srcs}")
    STRING(REGEX REPLACE " " ";" libslist "${libs}")
    add_executable(${execname} ${srcslist})
    target_link_libraries(${execname} ${libslist})
    DEFINE_DLL_IMPORTS(${execname} "${libslist}")  #add import definitions for all libs that the executable is linked to
    INSTALL(TARGETS ${execname} DESTINATION bin)
    # Enable extra compiler flags if local executables and/or global options dictate
    SET(LOCAL_COMPILE_FLAGS "")
    FOREACH(extraarg ${ARGN})
        IF(${extraarg} MATCHES "STRICT" AND SCL-ENABLE_STRICT)
            SET(LOCAL_COMPILE_FLAGS "${LOCAL_COMPILE_FLAGS} ${STRICT_FLAGS}")
        ENDIF(${extraarg} MATCHES "STRICT" AND SCL-ENABLE_STRICT)
    ENDFOREACH(extraarg ${ARGN})
    IF(LOCAL_COMPILE_FLAGS)
        SET_TARGET_PROPERTIES(${execname} PROPERTIES COMPILE_FLAGS ${LOCAL_COMPILE_FLAGS})
    ENDIF(LOCAL_COMPILE_FLAGS)
ENDMACRO(SCL_ADDEXEC execname srcs libs)

MACRO(SCL_ADDLIB libname srcs libs)
  STRING(REGEX REPLACE " " ";" srcslist "${srcs}")
  STRING(REGEX REPLACE " " ";" libslist1 "${libs}")
  STRING(REGEX REPLACE "-framework;" "-framework " libslist "${libslist1}")
  IF(SCL_BUILD_SHARED_LIBS)
      add_library(${libname} SHARED ${srcslist})
      DEFINE_DLL_EXPORTS(${libname})
      if(NOT ${libs} MATCHES "NONE")
          target_link_libraries(${libname} ${libslist})
          DEFINE_DLL_IMPORTS(${libname} "${libslist}" )
      endif(NOT ${libs} MATCHES "NONE")
      SET_TARGET_PROPERTIES(${libname} PROPERTIES VERSION ${SCL_ABI_VERSION} SOVERSION ${SCL_ABI_SOVERSION} )
      INSTALL(TARGETS ${libname} DESTINATION lib)
    if(APPLE)
        set_target_properties(${libname} PROPERTIES LINK_FLAGS "-flat_namespace -undefined suppress")
    endif(APPLE)
  ENDIF(SCL_BUILD_SHARED_LIBS)
  IF(SCL_BUILD_STATIC_LIBS AND NOT MSVC)
      add_library(${libname}-static STATIC ${srcslist})
      DEFINE_DLL_EXPORTS(${libname}-static)
      if(NOT ${libs} MATCHES "NONE")
          target_link_libraries(${libname}-static "${libslist}")
          DEFINE_DLL_IMPORTS(${libname}-static ${libslist} )
      endif(NOT ${libs} MATCHES "NONE")
      IF(NOT WIN32)
          SET_TARGET_PROPERTIES(${libname}-static PROPERTIES OUTPUT_NAME "${libname}")
      ENDIF(NOT WIN32)
      IF(WIN32)
          # We need the lib prefix on win32, so add it even if our add_library
          # wrapper function has removed it due to the target name - see
          # http://www.cmake.org/Wiki/CMake_FAQ#How_do_I_make_my_shared_and_static_libraries_have_the_same_root_name.2C_but_different_suffixes.3F
          SET_TARGET_PROPERTIES(${libname}-static PROPERTIES PREFIX "lib")
      ENDIF(WIN32)
      INSTALL(TARGETS ${libname}-static DESTINATION lib)
    if(APPLE)
        set_target_properties(${libname}-static PROPERTIES LINK_FLAGS "-flat_namespace -undefined suppress")
    endif(APPLE)
  ENDIF(SCL_BUILD_STATIC_LIBS AND NOT MSVC)
  # Enable extra compiler flags if local libraries and/or global options dictate
  SET(LOCAL_COMPILE_FLAGS "")
  FOREACH(extraarg ${ARGN})
      IF(${extraarg} MATCHES "STRICT" AND SCL-ENABLE_STRICT)
          SET(LOCAL_COMPILE_FLAGS "${LOCAL_COMPILE_FLAGS} ${STRICT_FLAGS}")
      ENDIF(${extraarg} MATCHES "STRICT" AND SCL-ENABLE_STRICT)
  ENDFOREACH(extraarg ${ARGN})
  IF(LOCAL_COMPILE_FLAGS)
      IF(BUILD_SHARED_LIBS)
          SET_TARGET_PROPERTIES(${libname} PROPERTIES COMPILE_FLAGS ${LOCAL_COMPILE_FLAGS})
      ENDIF(BUILD_SHARED_LIBS)
      IF(BUILD_STATIC_LIBS AND NOT MSVC)
          SET_TARGET_PROPERTIES(${libname}-static PROPERTIES COMPILE_FLAGS ${LOCAL_COMPILE_FLAGS})
      ENDIF(BUILD_STATIC_LIBS AND NOT MSVC)
  ENDIF(LOCAL_COMPILE_FLAGS)
ENDMACRO(SCL_ADDLIB libname srcs libs)
